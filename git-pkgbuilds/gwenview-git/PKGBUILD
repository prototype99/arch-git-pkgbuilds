# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

_pkgname=gwenview
pkgname=${_pkgname}-git
pkgver=0
pkgrel=1
pkgdesc="A fast and easy to use image viewer"
url='https://kde.org/applications/graphics/gwenview-git/'
arch=(x86_64)
license=(GPL LGPL FDL)
groups=(kde-applications-git kde-graphics-git kde-git)
depends=(kactivities-git baloo-git libkipi libkdcraw kparts-git kitemmodels-git cfitsio phonon-qt5-git purpose-git perl)
optdepends=('qt5-imageformats: support for tiff, webp, and more image formats'
            'kimageformats-git: support for dds, xcf, exr, psd, and more image formats'
            'kipi-plugins: export to various online services'
            'kamera: import pictures from gphoto2 cameras')
makedepends=(extra-cmake-modules-git kdoctools-git)
source=(${_pkgname}::git+https://anongit.kde.org/${_pkgname})
sha1sums=(SKIP )
options=(debug !strip)
conflicts=(${_pkgname})
provides=(${_pkgname})

pkgver() {
  cd $_pkgname
  printf "r%s.%s.%s" "$(git rev-list --count HEAD)" "$(git log -1 --date=format:"%Y%m%d" --format="%ad")" "$(git rev-parse --short HEAD)"
}

build() {
  cmake -B build -S ${_pkgname} \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DGWENVIEW_SEMANTICINFO_BACKEND="Baloo" \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
