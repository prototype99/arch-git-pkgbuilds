# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

_pkgname=kate
pkgname=(kwrite-git kate-git)
pkgver=0
pkgrel=1
arch=(x86_64)
license=(GPL LGPL FDL)
makedepends=(extra-cmake-modules-git kdoctools-git plasma-framework-git knewstuff-git kitemmodels-git ktexteditor-git kactivities-git kuserfeedback)
source=(${_pkgname}::git+https://anongit.kde.org/${_pkgname})
sha1sums=(SKIP )
options=(debug !strip)

pkgver() {
  cd $_pkgname
  printf "r%s.%s.%s" "$(git rev-list --count HEAD)" "$(git log -1 --date=format:"%Y%m%d" --format="%ad")" "$(git rev-parse --short HEAD)"
}

build() {
  cmake -B build -S ${_pkgname} \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DBUILD_TESTING=OFF \
    -DBUILD_tabswitcher=FALSE \
    -DBUILD_kate-ctags=FALSE \
    -DBUILD_backtracebrowser=FALSE \
    -DBUILD_xmltools=FALSE \
    -DBUILD_xmlcheck=FALSE \
    -DBUILD_openheader=FALSE \
    -DBUILD_gdbplugin=FALSE \
    -DBUILD_textfilter=FALSE \
    -DBUILD_rustcompletion=FALSE \
    -DBUILD_lumen=FALSE \
    -DBUILD_lspclient=FALSE \
    -DBUILD_katebuild-plugin=FALSE \
    -DBUILD_project=FALSE \
    -DBUILD_symbolviewer=FALSE \
    -DBUILD_replicode=FALSE \
    -DBUILD_close-except-like=FALSE \
    -DBUILD_snippets=FALSE \
    -DBUILD_preview=FALSE \
    -DBUILD_katesql=FALSE
  cmake --build build
}

package_kwrite-git() {
  pkgdesc="Text Editor"
  groups=(kde-applications-git kde-utilities-git kde-git)
  url="https://www.kde.org/applications/utilities/kwrite/"
  depends=(ktexteditor-git hicolor-icon-theme)
  conflicts=(kwrite)
  provides=(kwrite)

  DESTDIR="$pkgdir" cmake --install build

  find "$pkgdir" -type f -name '*kate*' -exec rm {} \;
  rm -r "$pkgdir"/usr/lib/qt/plugins/ktexteditor \
        "$pkgdir"/usr/share/doc/HTML/*/{kate,katepart} \
        "$pkgdir"/usr/share/plasma/plasmoids
}

package_kate-git() {
  pkgdesc="Advanced Text Editor"
  groups=(kde-applications-git kde-utilities-git kde-git)
  url="https://www.kde.org/applications/utilities/kate-git/"
  depends=(knewstuff-git ktexteditor-git threadweaver-git kactivities-git kuserfeedback hicolor-icon-theme)
  optdepends=('konsole-git: open a terminal in Kate'
              'clang: C and C++ LSP support'
              'python-language-server: Python LSP support'
              'texlab: LaTeX LSP support'
              'rust: Rust LSP support')
  conflicts=(kate)
  provides=(kate)

  DESTDIR="$pkgdir" cmake --install build

  find "$pkgdir" -type f -name '*kwrite*' -exec rm {} \;
  rm -r "$pkgdir"/usr/share/doc/HTML/*/kwrite
}
