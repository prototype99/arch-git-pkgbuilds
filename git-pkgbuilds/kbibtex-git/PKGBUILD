# Maintainer: Ray Rashif <schiv@archlinux.org>
# Maintainer: Antonio Rojas <nqn1976 @ gmail.com>
# Contributor: cmorlok <christianmorlok@web.de>

_pkgname=kbibtex
pkgname=${_pkgname}-git
pkgver=0
pkgrel=1
pkgdesc="A BibTeX editor for KDE"
arch=(x86_64)
url='https://userbase.kde.org/KBibTeX'
license=(GPL)
depends=(poppler-qt5 qt5-webengine qt5-xmlpatterns qt5-networkauth qt5-webkit qoauth ktexteditor-git kio-git icu)
optdepends=('okular-git: Document preview')
makedepends=(extra-cmake-modules-git kdoctools-git)
source=(${_pkgname}::git+https://anongit.kde.org/${_pkgname})
sha256sums=('SKIP')
groups=(kde-git)
options=(debug !strip)
conflicts=(${_pkgname})
provides=(${_pkgname})

pkgver() {
  cd $_pkgname
  printf "r%s.%s.%s" "$(git rev-list --count HEAD)" "$(git log -1 --date=format:"%Y%m%d" --format="%ad")" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${_pkgname} \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
