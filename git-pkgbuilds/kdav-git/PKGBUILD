# Maintainer: Antonio Rojas <arojas@archlinux.org>

_pkgname=kdav
pkgname=${_pkgname}-git
pkgver=0
pkgrel=1
pkgdesc="A DAV protocol implemention with KJobs"
url='https://community.kde.org/KDE_PIM'
arch=(x86_64)
license=(GPL LGPL FDL)
depends=(kio-git qt5-xmlpatterns)
makedepends=(extra-cmake-modules-git)
groups=(kde-git)
source=(${_pkgname}::git+https://anongit.kde.org/${_pkgname})
sha1sums=(SKIP )
options=(debug !strip)
conflicts=(${_pkgname})
provides=(${_pkgname})

pkgver() {
  cd $_pkgname
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cmake -B build -S ${_pkgname} \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
