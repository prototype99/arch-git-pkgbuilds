# Maintainer: Antonio Rojas <arojas@archlinux,org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

_pkgname=keditbookmarks
pkgname=${_pkgname}-git
pkgver=0
pkgrel=1
pkgdesc="Bookmark Organizer and Editor"
arch=(x86_64)
url="https://www.kde.org/"
license=(LGPL)
depends=(kparts-git)
makedepends=(extra-cmake-modules-git kdoctools-git)
groups=(kde-applications-git kdebase-git kde-git)
source=(${_pkgname}::git+https://anongit.kde.org/${_pkgname})
sha1sums=(SKIP )
options=(debug !strip)
conflicts=(keditbookmarks)
provides=(keditbookmarks)

pkgver() {
  cd $_pkgname
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${_pkgname} \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
