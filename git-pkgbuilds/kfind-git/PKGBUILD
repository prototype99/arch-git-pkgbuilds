# Maintainer: Antonio Rojas <arojas@archlinux,org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

_pkgname=kfind
pkgname=${_pkgname}-git
pkgver=0
pkgrel=1
pkgdesc="Find Files/Folders"
arch=(x86_64)
url="https://www.kde.org/applications/utilities/kfind-git/"
license=(LGPL)
depends=(kio-git kfilemetadata-git)
makedepends=(extra-cmake-modules-git kdoctools-git)
optdepends=('mlocate: search using mlocate index')
groups=(kde-applications-git kde-utilities-git kde-git)
source=(${_pkgname}::git+https://anongit.kde.org/${_pkgname})
sha1sums=(SKIP )
options=(debug !strip)
conflicts=(${_pkgname})
provides=(${_pkgname})

pkgver() {
  cd $_pkgname
  printf "r%s.%s.%s" "$(git rev-list --count HEAD)" "$(git log -1 --date=format:"%Y%m%d" --format="%ad")" "$(git rev-parse --short HEAD)"
}

build() {
  cmake -B build -S ${_pkgname} \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
