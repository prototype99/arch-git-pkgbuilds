# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

_pkgname=kio-extras
pkgname=${_pkgname}-git
pkgver=0
pkgrel=1
pkgdesc="Additional components to increase the functionality of KIO"
arch=(x86_64)
url='https://www.kde.org/applications/internet/'
license=(LGPL)
depends=(kio-git kdnssd-git libssh smbclient exiv2 openexr libmtp kpty-git kactivities-stats-git phonon-qt5-git syntax-highlighting-git kdsoap-ws-discovery-client libxcursor)
makedepends=(extra-cmake-modules-git kdoctools-git gperf taglib libappimage)
optdepends=('qt5-imageformats: thumbnails for additional image formats' 'perl: info kioslave'
            'kimageformats-git: thumbnails for additional image formats' 'taglib: audio file thumbnails'
            'libappimage: AppImage thumbnails' 'icoutils: Windows executable thumbnails')
groups=(kde-applications-git kde-network-git kde-git)
source=(${_pkgname}::git+https://anongit.kde.org/${_pkgname})
sha1sums=(SKIP )
options=(debug !strip)
conflicts=(${_pkgname})
provides=(${_pkgname})

pkgver() {
  cd $_pkgname
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cmake -B build -S ${_pkgname} \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DLIBAPPIMAGE_LIBRARIES=libappimage.so \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
