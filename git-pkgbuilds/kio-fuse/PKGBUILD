# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

_pkgname=kio-fuse
pkgname=${_pkgname}-git
pkgver=0
pkgrel=1
pkgdesc='Resource and network access abstraction'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(kio-git kcoreaddons-git kdbusaddons-git fuse3)
makedepends=(extra-cmake-modules-git kdoctools-git doxygen qt5-tools)
optdepends=('kio-extras-git: extra protocols support (sftp, fish and more)' 'kdoctools-git: for the help kioslave'
            'knetattach-git: to add new kio-remote entries')
groups=(kf5-git kde-git)
source=(${_pkgname}::git+https://anongit.kde.org/${_pkgname})
sha1sums=(SKIP )
options=(debug !strip)
conflicts=($_pkgname)
provides=($_pkgname)

pkgver() {
  cd $_pkgname
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${_pkgname} \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
