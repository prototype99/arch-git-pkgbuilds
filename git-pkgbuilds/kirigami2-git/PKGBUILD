# Maintainer: Antonio Rojas <arojas@archlinux.org>

_pkgname=kirigami2
pkgname=${_pkgname}-git
pkgver=0
pkgrel=1
pkgdesc='A QtQuick based components set'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
groups=(kf5-git kde-git)
depends=(qt5-quickcontrols qt5-quickcontrols2 qt5-graphicaleffects)
makedepends=(extra-cmake-modules-git qt5-tools qt5-svg kpackage-git doxygen)
source=(${_pkgname}::git+https://anongit.kde.org/${_pkgname%2})
sha1sums=(SKIP )
options=(debug !strip)
conflicts=(kirigami2)
provides=(kirigami2)

pkgver() {
  cd $_pkgname
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${_pkgname} \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}

