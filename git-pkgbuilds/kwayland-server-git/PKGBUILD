# Maintainer:Antonio Rojas <arojas@archlinux.org>

_pkgname=kwayland-server
pkgname=${_pkgname}-git
pkgver=0
pkgrel=1
pkgdesc='Wayland server components built on KDE Frameworks'
arch=(x86_64)
url='https://kde.org/plasma-desktop-git'
license=(LGPL)
depends=(qt5-base)
makedepends=(extra-cmake-modules-git plasma-wayland-protocols-git wayland-protocols kwayland-git doxygen qt5-tools)
groups=(plasma-git kde-git)
source=(${_pkgname}::git+https://anongit.kde.org/${_pkgname})
sha1sums=(SKIP )
options=(debug !strip)
conflicts=(${_pkgname})
provides=(${_pkgname})

pkgver() {
  cd $_pkgname
  printf "r%s.%s.%s" "$(git rev-list --count HEAD)" "$(git log -1 --date=format:"%Y%m%d" --format="%ad")" "$(git rev-parse --short HEAD)"
}

build() {
  cmake -B build -S ${_pkgname} \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
