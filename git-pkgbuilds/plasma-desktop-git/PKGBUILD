# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

_pkgname=plasma-desktop
pkgname=(plasma-desktop-git knetattach-git)
pkgver=0
pkgrel=1
pkgdesc='KDE Plasma Desktop'
arch=(x86_64)
url='https://www.kde.org/workspaces/plasmadesktop/'
license=(LGPL)
source=(${_pkgname}::git+https://anongit.kde.org/${_pkgname})
depends=(polkit-kde-agent-git libxkbfile kmenuedit-git systemsettings-git ksysguard-git baloo-git libibus accountsservice)
makedepends=(extra-cmake-modules-git kdoctools-git xf86-input-evdev xf86-input-synaptics xf86-input-libinput xorg-server-devel
             scim kdesignerplugin-git kaccounts-integration-git intltool)
groups=(plasma-git kde-git)
sha1sums=(SKIP )
options=(debug !strip)

pkgver() {
  cd $_pkgname
  printf "r%s.%s.%s" "$(git rev-list --count HEAD)" "$(git log -1 --date=format:"%Y%m%d" --format="%ad")" "$(git rev-parse --short HEAD)"
}

build() {
  cmake -B build -S ${_pkgname} \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package_plasma-desktop-git() {
  depends+=(knetattach-git)
  optdepends=('plasma-nm-git: Network manager applet'
              'powerdevil-git: power management, suspend and hibernate support'
              'kscreen-git: screen management'
              'ibus: kimpanel IBUS support'
              'scim: kimpanel SCIM support'
              'kaccounts-integration-git: OpenDesktop integration plugin')
  conflicts=(plasma-desktop)
  provides=(plasma-desktop)

  DESTDIR="$pkgdir" cmake --install build

# Split knetattach-git
  rm "$pkgdir"/usr/{bin/knetattach,share/applications/org.kde.knetattach.desktop}
}

package_knetattach-git() {
  pkgdesc='Wizard which makes it easier to integrate network resources with the Plasma Desktop'
  depends=(kdelibs4support-git)
  conflicts=(knetattach)
  provides=(knetattach)

  DESTDIR="$pkgdir" cmake --install build/knetattach
}
