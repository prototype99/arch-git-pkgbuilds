# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

_pkgname=plasma-nm
pkgname=${_pkgname}-git
pkgver=0
pkgrel=1
pkgdesc='Plasma applet written in QML for managing network connections'
arch=(x86_64)
url='https://kde.org/plasma-desktop-git/'
license=(GPL2)
depends=(plasma-workspace-git modemmanager-qt-git networkmanager-qt-git qca-qt5)
makedepends=(extra-cmake-modules-git openconnect)
optdepends=('openconnect: Cisco AnyConnect VPN plugin')
groups=(plasma-git kde-git)
source=(${_pkgname}::git+https://anongit.kde.org/${_pkgname})
sha1sums=(SKIP )
options=(debug !strip)
conflicts=(${_pkgname})
provides=(${_pkgname})

pkgver() {
  cd $_pkgname
  printf "r%s.%s.%s" "$(git rev-list --count HEAD)" "$(git log -1 --date=format:"%Y%m%d" --format="%ad")" "$(git rev-parse --short HEAD)"
}

build() {
  cmake -B build -S ${_pkgname} \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
