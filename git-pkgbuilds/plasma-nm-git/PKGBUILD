# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

_pkgname=plasma-nm
pkgname=${_pkgname}-git
pkgver=0
pkgrel=1
pkgdesc='Plasma applet written in QML for managing network connections'
arch=(x86_64)
url='https://www.kde.org/workspaces/plasmadesktop/'
license=(GPL2)
depends=(plasma-workspace-git modemmanager-qt-git networkmanager-qt-git qca mobile-broadband-provider-info)
makedepends=(extra-cmake-modules-git openconnect)
optdepends=('openconnect: Cisco AnyConnect VPN plugin')
groups=(plasma-git kde-git)
source=(${_pkgname}::git+https://anongit.kde.org/${_pkgname}
         kdebug-423093.patch::"https://invent.kde.org/plasma/plasma-nm-git/commit/271332cf.patch")
sha1sums=(SKIP SKIP )
options=(debug !strip)
conflicts=(${_pkgname})
provides=(${_pkgname})

pkgver() {
  cd $_pkgname
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  patch -d ${_pkgname} -p1 -i ../kdebug-423093.patch # Fix plasma-nm-git asking twice for password
}

build() {
  cmake -B build -S ${_pkgname} \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
