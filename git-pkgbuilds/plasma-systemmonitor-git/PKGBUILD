# Maintainer: Antonio Rojas <arojas@archlinux.org>

_pkgname=plasma-systemmonitor
pkgname=${_pkgname}-git
pkgver=0
pkgrel=1
pkgdesc='An interface for monitoring system sensors, process information and other system resources'
arch=(x86_64)
url='https://kde.org/plasma-desktop-git/'
license=(GPL LGPL)
source=(${_pkgname}::git+https://anongit.kde.org/${_pkgname})
depends=(ksystemstats-git kitemmodels-git qqc2-desktop-style-git kquickcharts-git)
makedepends=(extra-cmake-modules-git)
groups=(plasma-git kde-git)
sha1sums=(SKIP )
options=(debug !strip)
conflicts=(${_pkgname})
provides=(${_pkgname})

pkgver() {
  cd $_pkgname
  printf "r%s.%s.%s" "$(git rev-list --count HEAD)" "$(git log -1 --date=format:"%Y%m%d" --format="%ad")" "$(git rev-parse --short HEAD)"
}

build() {
  cmake -B build -S ${_pkgname} \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
