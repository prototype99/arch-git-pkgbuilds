# Maintainer: Antonio Rojas <arojas@archlinux.org>

_pkgname=plasma-wayland-protocols
pkgname=${_pkgname}-git
pkgver=0
pkgrel=1
pkgdesc="Plasma Specific Protocols for Wayland"
arch=(any)
url="https://kde.org/plasma-desktop-git"
license=(LGPL)
depends=()
makedepends=(qt5-base extra-cmake-modules-git)
groups=(kde-git)
source=(${_pkgname}::git+https://anongit.kde.org/${_pkgname})
sha1sums=(SKIP )
options=(debug !strip)
conflicts=(${_pkgname})
provides=(${_pkgname})

pkgver() {
  cd $_pkgname
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${_pkgname} \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DBUILD_QCH=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
