# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Alexey D. <lq07829icatm at rambler.ru>

_pkgname=plasma-workspace
pkgname=(plasma-workspace-git plasma-wayland-session-git)
pkgver=0
pkgrel=1
pkgdesc='KDE Plasma Workspace'
arch=(x86_64)
url='https://kde.org/plasma-desktop-git/'
license=(LGPL)
depends=(knotifyconfig-git ksystemstats-git ktexteditor-git libqalculate appstream-qt
         xorg-xrdb xorg-xsetroot kactivitymanagerd-git kholidays-git xorg-xmessage milou-git prison-git kwin-git
         plasma-integration-git kpeople-git kactivities-stats-git libkscreen-git kquickcharts-git kuserfeedback kio-extras-git kio-fuse-git)
makedepends=(extra-cmake-modules-git kdoctools-git gpsd baloo-git networkmanager-qt-git plasma-wayland-protocols-git kunitconversion-git kinit-git)
groups=(plasma-git kde-git)
source=(${_pkgname}::git+https://anongit.kde.org/${_pkgname} kde.pam)
sha1sums=(SKIP SKIP )
options=(debug !strip)

pkgver() {
  cd $_pkgname
  printf "r%s.%s.%s" "$(git rev-list --count HEAD)" "$(git log -1 --date=format:"%Y%m%d" --format="%ad")" "$(git rev-parse --short HEAD)"
}

build() {
  cmake -B build -S ${_pkgname} \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package_plasma-workspace-git() {
  optdepends=('plasma-workspace-wallpapers-git: additional wallpapers'
              'gpsd: GPS based geolocation' 'networkmanager-qt-git: IP based geolocation'
              'kdepim-addons: displaying PIM events in the calendar'
              'appmenu-gtk-module: global menu support for GTK2 and some GTK3 applications'
              'baloo-git: Baloo search runner' 'discover-git: manage applications installation from the launcher')
  backup=('etc/pam.d/kde')
  conflicts=(plasma-workspace)
  provides=(plasma-workspace)

  DESTDIR="$pkgdir" cmake --install build

  install -Dm644 "$srcdir"/kde.pam "$pkgdir"/etc/pam.d/kde

  # Split plasma-wayland scripts
  rm -r "$pkgdir"/usr/share/wayland-sessions
}

package_plasma-wayland-session-git() {
  pkgdesc='Plasma Wayland session'
  depends=(plasma-workspace-git qt5-wayland kwayland-integration-git xorg-xwayland)
  conflicts=(plasma-wayland-session)
  provides=(plasma-wayland-session)

  install -Dm644 build/login-sessions/plasmawayland.desktop "$pkgdir"/usr/share/wayland-sessions/plasmawayland.desktop
}
