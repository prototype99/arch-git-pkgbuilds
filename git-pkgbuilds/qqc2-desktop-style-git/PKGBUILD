# Maintainer: Antonio Rojas <arojas@archlinux.org>

_pkgname=qqc2-desktop-style
pkgname=${_pkgname}-git
pkgver=0
pkgrel=1
pkgdesc='A style for Qt Quick Controls 2 to make it follow your desktop theme'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
groups=(kf5-git kde-git)
depends=(kirigami2-git kiconthemes-git)
makedepends=(extra-cmake-modules-git)
source=(${_pkgname}::git+https://anongit.kde.org/${_pkgname})
sha1sums=(SKIP )
options=(debug !strip)
conflicts=(${_pkgname})
provides=(${_pkgname})

pkgver() {
  cd $_pkgname
  printf "r%s.%s.%s" "$(git rev-list --count HEAD)" "$(git log -1 --date=format:"%Y%m%d" --format="%ad")" "$(git rev-parse --short HEAD)"
}

build() {
  cmake -B build -S ${_pkgname} \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
