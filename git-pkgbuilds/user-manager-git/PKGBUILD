# Maintainer:Antonio Rojas <arojas@archlinux.org>

_pkgname=user-manager
pkgname=${_pkgname}-git
pkgver=0
pkgrel=1
pkgdesc='A simple system settings module to manage the users of your system'
arch=(x86_64)
url='https://www.kde.org/workspaces/plasmadesktop/'
license=(LGPL)
depends=(accountsservice libpwquality kio-git)
makedepends=(extra-cmake-modules-git kcmutils-git)
groups=(plasma-git kde-git)
source=(${_pkgname}::git+https://anongit.kde.org/${_pkgname})
sha1sums=(SKIP )
options=(debug !strip)
conflicts=(${_pkgname})
provides=(${_pkgname})

pkgver() {
  cd $_pkgname
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cmake -B build -S ${_pkgname} \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
