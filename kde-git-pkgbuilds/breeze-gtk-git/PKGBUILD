# Maintainer: Antonio Rojas <arojas@archlinux.org>

_pkgname=breeze-gtk
pkgname=${_pkgname}-git
pkgver=0
pkgrel=3
pkgdesc="Breeze widget theme for GTK 2 and 3"
arch=(x86_64)
url='https://quickgit.kde.org/?p=breeze-gtk-git.git'
license=(LGPL)
depends=(qt5-base)
makedepends=(extra-cmake-modules-git sassc python-cairo breeze-git)
groups=(plasma-git kde-git)
source=(${_pkgname}::git+https://anongit.kde.org/${_pkgname})
sha1sums=(SKIP )
options=(debug !strip)
conflicts=(breeze-gtk)
provides=(breeze-gtk)

pkgver() {
  cd $_pkgname
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare(){
  mkdir -p build
}

build(){
  cd build
  cmake ../${_pkgname} \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
