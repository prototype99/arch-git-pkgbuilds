# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

_pkgname=kdesignerplugin
pkgname=${_pkgname}-git
pkgver=0
pkgrel=3
pkgdesc='Integration of Frameworks widgets in Qt Designer/Creator'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(kplotting-git kio-git)
makedepends=(extra-cmake-modules-git qt5-tools kdoctools-git kdewebkit-git)
optdepends=('kdewebkit-git: KDEWebKit plugin')
groups=(kf5-git kde-git)
source=(${_pkgname}::git+https://anongit.kde.org/${_pkgname})
sha1sums=(SKIP )
options=(debug !strip)
conflicts=(kdesignerplugin)
provides=(kdesignerplugin)

pkgver() {
  cd $_pkgname
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${_pkgname} \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
