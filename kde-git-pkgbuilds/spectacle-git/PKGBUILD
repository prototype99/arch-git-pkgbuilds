# Maintainer: Antonio Rojas <arojas@archlinux.org>

_pkgname=spectacle
pkgname=${_pkgname}-git
pkgver=0
pkgrel=1
pkgdesc="KDE screenshot capture utility"
arch=(x86_64)
url='https://www.kde.org/applications/graphics/'
license=(GPL)
depends=(xcb-util-cursor libkipi purpose-git knewstuff-git qt5-quickcontrols)
makedepends=(extra-cmake-modules-git kdoctools-git)
optdepends=('kipi-plugins: export to various online services')
groups=(kde-applications-git kdegraphics-git kde-git)
source=(${_pkgname}::git+https://anongit.kde.org/${_pkgname})
sha1sums=(SKIP)
options=(debug !strip)
conflicts=(spectacle)
provides=(spectacle)

pkgver() {
  cd $_pkgname
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${_pkgname} \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
   -DCMAKE_INSTALL_PREFIX=/usr \
   -DCMAKE_INSTALL_LIBDIR=lib \
   -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}

