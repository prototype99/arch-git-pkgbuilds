# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

_pkgname=systemsettings
pkgname=${_pkgname}-git
pkgver=0
pkgrel=3
pkgdesc='KDE system manager for hardware, software, and workspaces'
arch=(x86_64)
url='https://www.kde.org/workspaces/plasmadesktop/'
license=(LGPL)
depends=(kactivities-stats-git plasma-workspace-git)
makedepends=(extra-cmake-modules-git kdoctools-git khtml-git)
optdepends=('khtml-git: for the tree view')
groups=(plasma-git kde-git)
source=(${_pkgname}::git+https://anongit.kde.org/${_pkgname})
sha1sums=(SKIP )
options=(debug !strip)
conflicts=(systemsettings)
provides=(systemsettings)

pkgver() {
  cd $_pkgname
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${_pkgname} \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
