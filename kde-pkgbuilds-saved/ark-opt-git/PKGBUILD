# Maintainer: Antonio Rojas <arojas@archlinux.org> 
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

_pkgname=ark
pkgname=${_pkgname}-opt-git
pkgver=0
pkgrel=1
pkgdesc='Archiving Tool'
arch=(x86_64)
url='https://kde.org/applications/utilities/ark-opt-git/'
license=(GPL)
depends=(kparts-opt-git kpty-opt-git libarchive libzip kitemmodels-opt-git hicolor-icon-theme)
makedepends=(extra-cmake-modules-opt-git kdoctools-opt-git)
optdepends=('p7zip: 7Z format support' 'unrar: RAR decompression support' 'unarchiver: RAR format support'
            'lzop: LZO format support' 'lrzip: LRZ format support')
groups=(kde-applications-opt kdeutils-opt kde-opt)
source=(${_pkgname}::git+https://anongit.kde.org/${_pkgname})
sha1sums=(SKIP )
options=(debug !strip)

pkgver() {
  cd $_pkgname
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() { 
  export PATH="/opt/kde/bin:${PATH}"
  export XDG_DATA_DIRS="/opt/kde/share:${XDG_DATA_DIRS}"

  cd build
  cmake ../${_pkgname} \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DCMAKE_INSTALL_PREFIX=/opt/kde \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}

