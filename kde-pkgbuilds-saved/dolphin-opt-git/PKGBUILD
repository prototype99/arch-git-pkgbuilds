# Maintainer: Antonio Rojas <arojas@archlinux,org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

_pkgname=dolphin
pkgname=${_pkgname}-opt-git
pkgver=0
pkgrel=1
pkgdesc="KDE File Manager"
arch=(x86_64)
url="https://kde.org/applications/system/dolphin-opt-git/"
license=(LGPL)
depends=(baloo-widgets-opt-git knewstuff-opt-git kio-extras-opt-git kcmutils-opt-git kparts-opt-git kinit-opt-git phonon-qt5-opt-git)
makedepends=(extra-cmake-modules-opt-git kdoctools-opt-git)
optdepends=('kde-cli-tools-opt-git: for editing file type options' 'ffmpegthumbs: video thumbnails' 
            'ruby: installing new service menus from KDE Store' 'kdegraphics-thumbnailers: PDF and PS thumbnails'
            'konsole-opt-git: terminal panel' 'purpose-opt-git: share context menu')
groups=(kde-applications-opt kdebase-opt kde-opt)
source=(${_pkgname}::git+https://anongit.kde.org/${_pkgname})
sha1sums=(SKIP )
options=(debug !strip)

pkgver() {
  cd $_pkgname
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  export PATH="/opt/kde/bin:${PATH}"
  export XDG_DATA_DIRS="/opt/kde/share:${XDG_DATA_DIRS}"

  cd build
  cmake ../${_pkgname} \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DCMAKE_INSTALL_PREFIX=/opt/kde \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
