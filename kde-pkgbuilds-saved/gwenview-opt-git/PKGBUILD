# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

_pkgname=gwenview
pkgname=${_pkgname}-opt-git
pkgver=0
pkgrel=1
pkgdesc="A fast and easy to use image viewer"
url='https://kde.org/applications/graphics/gwenview-opt-git/'
arch=(x86_64)
license=(GPL LGPL FDL)
groups=(kde-applications-opt kdegraphics-opt kde-opt)
depends=(kactivities-opt-git baloo-opt-git libkipi libkdcraw kparts-opt-git kitemmodels-opt-git cfitsio phonon-qt5-opt-git)
optdepends=('qt5-imageformats: support for tiff, webp, and more image formats'
            'kimageformats-opt-git: support for dds, xcf, exr, psd, and more image formats'
            'kipi-plugins: export to various online services'
            'kamera: import pictures from gphoto2 cameras')
makedepends=(extra-cmake-modules-opt-git kdoctools-opt-git)
source=(${_pkgname}::git+https://anongit.kde.org/${_pkgname})
sha1sums=(SKIP )
options=(debug !strip)

pkgver() {
  cd $_pkgname
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  export PATH="/opt/kde/bin:${PATH}"
  export XDG_DATA_DIRS="/opt/kde/share:${XDG_DATA_DIRS}"

  cd build
  cmake ../${_pkgname} \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DCMAKE_INSTALL_PREFIX=/opt/kde \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DGWENVIEW_SEMANTICINFO_BACKEND="Baloo" \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
