# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

_pkgname=kate
pkgname=(kwrite-opt-git kate-opt-git)
pkgver=0
pkgrel=1
arch=(x86_64)
license=(GPL LGPL FDL)
makedepends=(extra-cmake-modules-opt-git kdoctools-opt-git plasma-framework-opt-git knewstuff-opt-git ktexteditor-opt-git threadweaver-opt-git kitemmodels-opt-git kactivities-opt-git)
source=(${_pkgname}::git+https://anongit.kde.org/${_pkgname})
sha1sums=(SKIP )
options=(debug !strip)

pkgver() {
  cd $_pkgname
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  export PATH="/opt/kde/bin:${PATH}"
  export XDG_DATA_DIRS="/opt/kde/share:${XDG_DATA_DIRS}"

  cd build
  cmake ../${_pkgname} \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DCMAKE_INSTALL_PREFIX=/opt/kde \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package_kwrite-opt-git() {
  pkgdesc="Text Editor"
  groups=(kde-applications-opt kdebase-opt kde-opt)
  url="https://www.kde.org/applications/utilities/kwrite/"
  depends=(ktexteditor-opt-git kactivities-opt-git hicolor-icon-theme)

  cd build
  make DESTDIR="$pkgdir" install

  find "$pkgdir" -type f -name '*kate*' -exec rm {} \;
  rm -r "$pkgdir"/opt/kde/share/doc/HTML/*/{kate,katepart} \
        "$pkgdir"/opt/kde/share/katexmltools \
        "$pkgdir"/opt/kde/lib/plugins/ktexteditor \
        "$pkgdir"/opt/kde/share/plasma/plasmoids
}

package_kate-opt-git() {
  pkgdesc="Advanced Text Editor"
  groups=(kde-applications-opt kdebase-opt kde-opt)
  url="https://www.kde.org/applications/utilities/kate-opt-git/"
  depends=(knewstuff-opt-git ktexteditor-opt-git threadweaver-opt-git kitemmodels-opt-git kactivities-opt-git hicolor-icon-theme)
  optdepends=('konsole-opt-git: open a terminal in Kate')

  cd build
  make DESTDIR="$pkgdir" install

  find "$pkgdir" -type f -name '*kwrite*' -exec rm {} \;
  rm -r "$pkgdir"/opt/kde/share/doc/HTML/*/kwrite
}
