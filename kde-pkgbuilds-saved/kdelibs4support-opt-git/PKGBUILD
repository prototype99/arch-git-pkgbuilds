# $Id: PKGBUILD 332123 2018-08-17 21:14:22Z arojas $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

_pkgname=kdelibs4support
pkgname=${_pkgname}-opt-git
pkgver=5.49.0
pkgrel=1
pkgdesc='Porting aid from KDELibs4'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(kunitconversion-opt-git kitemmodels-opt-git kemoticons-opt-git kded-opt-git kparts-opt-git)
makedepends=(extra-cmake-modules-opt-git kdoctools-opt-git qt5-tools networkmanager perl-uri kdesignerplugin-opt-git)
groups=(kf5-aids-opt kde-opt)
source=(${_pkgname}::git+https://anongit.kde.org/${_pkgname})
sha1sums=(SKIP )
options=(debug !strip)

pkgver() {
  cd $_pkgname
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  export PATH="/opt/kde/bin:${PATH}"
  export XDG_DATA_DIRS="/opt/kde5/share:${XDG_DATA_DIRS}"

  cd build
  cmake ../${_pkgname} \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DCMAKE_INSTALL_PREFIX=/opt/kde \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install

# cert bundle seems to be hardcoded
# link it to the one from ca-certificates
  rm -f "$pkgdir"/opt/kde/share/kf5/kssl/ca-bundle.crt
  ln -sf /etc/ssl/certs/ca-certificates.crt "$pkgdir"/opt/kde/share/kf5/kssl/ca-bundle.crt
}
