# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

_pkgname=kiconthemes
pkgname=${_pkgname}-opt-git
pkgver=0
pkgrel=1
pkgdesc='Support for icon themes'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(qt5-svg kconfigwidgets-opt-git kitemviews-opt-git karchive-opt-git)
makedepends=(extra-cmake-modules-opt-git doxygen qt5-tools)
optdepends=('breeze-icons-opt-git: fallback icon theme')
groups=(kf5-opt kde-opt)
source=(${_pkgname}::git+https://anongit.kde.org/${_pkgname})
sha1sums=(SKIP )
options=(debug !strip)

pkgver() {
  cd $_pkgname
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  export PATH="/opt/kde/bin:${PATH}"
  export XDG_DATA_DIRS="/opt/kde/share:${XDG_DATA_DIRS}"

  cd build
  cmake ../${_pkgname} \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DCMAKE_INSTALL_PREFIX=/opt/kde \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
