# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

_pkgname=kinfocenter
pkgname=${_pkgname}-opt-git
pkgver=0
pkgrel=1
arch=(x86_64)
url='https://www.kde.org/applications/system/kinfocenter-opt-git/'
license=(LGPL)
depends=(kcmutils-opt-git pciutils glu libraw1394 kwayland-opt-git)
makedepends=(extra-cmake-modules-opt-git plasma-framework-opt-git kdoctools-opt-git)
groups=(plasma-opt kde-opt)
source=(${_pkgname}::git+https://anongit.kde.org/${_pkgname} 
        https://sources.archlinux.org/other/artwork/archlinux-artwork-1.6.tar.gz
        kcm-about-distrorc)
sha1sums=(SKIP SKIP SKIP )
options=(debug !strip)

pkgver() {
  cd $_pkgname
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  export PATH="/opt/kde/bin:${PATH}"
  export XDG_DATA_DIRS="/opt/kde/share:${XDG_DATA_DIRS}"

  cd build
  cmake ../${_pkgname} \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DCMAKE_INSTALL_PREFIX=/opt/kde \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install

# Install Arch logo
  install -Dm644 "$srcdir"/archlinux-artwork-1.6/logos/archlinux-vert-dark.svg \
    "$pkgdir"/opt/kde/share/about-distro/archlinux-logo.svg
  install -Dm644 "$srcdir"/kcm-about-distrorc "$pkgdir"/opt/kde/etc/xdg/kcm-about-distrorc
}
