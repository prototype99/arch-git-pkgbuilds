# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

_pkgname=kio-extras
pkgname=${_pkgname}-opt-git
pkgver=0
pkgrel=1
pkgdesc="Additional components to increase the functionality of KIO"
arch=(x86_64)
url='https://www.kde.org/applications/internet/'
license=(LGPL)
depends=(kio-opt-git kdnssd-opt-git libssh smbclient exiv2 openexr libmtp kpty-opt-git kactivities-opt-git syntax-highlighting-opt-git)
makedepends=(extra-cmake-modules-opt-git kdoctools-opt-git gperf taglib khtml-opt-git libappimage)
optdepends=('qt5-imageformats: thumbnails for additional image formats'
            'kimageformats-opt-git: thumbnails for additional image formats' 'taglib: audio file thumbnails'
            'khtml-opt-git: man kioslave' 'phonon-qt5-opt-git: audio file previews' 'libappimage: AppImage thumbnails')
groups=(kde-applications-opt kdenetwork-opt kde-opt)
source=(${_pkgname}::git+https://anongit.kde.org/${_pkgname}
        kio-extras-rpc.patch)
sha1sums=(SKIP SKIP )
options=(debug !strip)

pkgver() {
  cd $_pkgname
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build

  cd ${_pkgname}
  patch -p1 -i ../kio-extras-rpc.patch # fix build of kio-nfs with glibc 2.27
}

build() {
  export PATH="/opt/kde/bin:${PATH}"
  export XDG_DATA_DIRS="/opt/kde/share:${XDG_DATA_DIRS}"

  cd build
  cmake ../${_pkgname} \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DCMAKE_INSTALL_PREFIX=/opt/kde \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DLIBAPPIMAGE_LIBRARIES=libappimage.so \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
