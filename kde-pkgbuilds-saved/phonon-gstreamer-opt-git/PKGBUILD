# $Id: PKGBUILD 316508 2018-02-09 11:15:52Z arojas $
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

_pkgname=phonon-gstreamer
pkgname=(phonon-qt5-gstreamer-opt-git)
pkgver=r3260.d1c45eed
pkgrel=1
arch=(x86_64)
url='http://phonon.kde.org/'
license=(LGPL)
optdepends=('pulseaudio: PulseAudio support' 'gst-plugins-good: PulseAudio support and good codecs' 'gst-plugins-bad: additional codecs'
	'gst-plugins-ugly: additional codecs' 'gst-libav: libav codec')
makedepends=(gst-plugins-base extra-cmake-modules-opt-git phonon-qt5-opt-git qt5-x11extras)
source=(${_pkgname}::git+https://anongit.kde.org/${_pkgname})
sha1sums=(SKIP )
options=(debug !strip)

pkgver() {
  cd $_pkgname
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir build-qt5
}

build() {
  export PATH="/opt/kde/bin:${PATH}"
  export XDG_DATA_DIRS="/opt/kde5/share:${XDG_DATA_DIRS}"

  cd build-qt5
  cmake ../${_pkgname} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DCMAKE_INSTALL_PREFIX=/opt/kde \
    -DCMAKE_SKIP_RPATH=ON \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -D__KDE_HAVE_GCC_VISIBILITY=NO \
    -DPHONON_BUILD_PHONON4QT5=ON
  make
}

package_phonon-qt5-gstreamer-opt-git() {
  pkgdesc="Phonon GStreamer backend for Qt5"
  depends=(gst-plugins-base qt5-x11extras phonon-qt5-opt-git)

  cd build-qt5
  make DESTDIR="$pkgdir" install
}
