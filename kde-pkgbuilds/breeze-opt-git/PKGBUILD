# $Id: PKGBUILD 328178 2018-07-10 13:33:04Z arojas $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

_pkgname=breeze
pkgname=(breeze-opt-git breeze-kde4-opt-git)
pkgver=5.13.3
pkgrel=1
arch=(x86_64)
url='https://www.kde.org/workspaces/plasmadesktop/'
license=(LGPL)
makedepends=(extra-cmake-modules-opt-git frameworkintegration-opt-git kdelibs automoc4 kdecoration-opt-git kcmutils-opt-git plasma-framework-opt-git)
source=(${_pkgname}::git+https://anongit.kde.org/${_pkgname})
sha1sums=(SKIP )
options=(debug !strip)

pkgver() {
  cd $_pkgname
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build{,-kde4}
}

build() {
  export PATH="/opt/kde/bin:${PATH}"
  export XDG_DATA_DIRS="/opt/kde5/share:${XDG_DATA_DIRS}"

  cd build
  cmake ../${_pkgname} \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DCMAKE_INSTALL_PREFIX=/opt/kde \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make

  cd ../build-kde4
  cmake ../${_pkgname} \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DCMAKE_INSTALL_PREFIX=/opt/kde \
    -DUSE_KDE4=ON \
    -DBUILD_TESTING=OFF
  make
}

package_breeze-opt-git() {
  depends=(frameworkintegration-opt-git kdecoration-opt-git breeze-icons-opt-git kwayland-opt-git hicolor-icon-theme)
  pkgdesc='Artwork, styles and assets for the Breeze visual style for the Plasma Desktop'
  optdepends=('breeze-kde4-opt-git: Breeze widget style for KDE4 applications'
		'breeze-gtk-opt-git: Breeze widget style for GTK applications'
		'kcmutils-opt-git: for breeze-settings')
  groups=(plasma-opt)

  cd build
  make DESTDIR="$pkgdir" install
}

package_breeze-kde4-opt-git() {
  pkgdesc='Breeze widget style for KDE4 applications'
  depends=(kdelibs)

  cd build-kde4
  make DESTDIR="$pkgdir" install

  # needed for pure Qt4 apps
  install -d "$pkgdir"/opt/kde/lib/qt4/plugins/styles
  ln -s /opt/kde/lib/kde4/plugins/styles/breeze-opt-git.so "$pkgdir"/opt/kde/lib/qt4/plugins/styles
}
