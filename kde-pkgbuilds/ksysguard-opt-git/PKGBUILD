# $Id: PKGBUILD 330542 2018-08-04 17:08:45Z felixonmars $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

_pkgname=ksysguard
pkgname=${_pkgname}-opt-git
pkgver=5.13.4
pkgrel=1
pkgdesc='Track and control the processes running in your system'
arch=(x86_64)
url='https://userbase.kde.org/KSysGuard'
license=(LGPL)
depends=(knewstuff-opt-git libksysguard-opt-git hicolor-icon-theme kinit-opt-git)
makedepends=(extra-cmake-modules-opt-git kdoctools-opt-git)
groups=(plasma-opt kde-opt)
source=(${_pkgname}::git+https://anongit.kde.org/${_pkgname})
sha1sums=(SKIP )
options=(debug !strip)

pkgver() {
  cd $_pkgname
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  export PATH="/opt/kde/bin:${PATH}"
  export XDG_DATA_DIRS="/opt/kde5/share:${XDG_DATA_DIRS}"

  cd build
  cmake ../${_pkgname} \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DCMAKE_INSTALL_PREFIX=/opt/kde \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
