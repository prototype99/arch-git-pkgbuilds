# $Id: PKGBUILD 328178 2018-07-10 13:33:04Z arojas $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

_pkgname=oxygen
pkgname=(oxygen-opt-git oxygen-kde4-opt-git)
pkgver=5.13.3
pkgrel=1
arch=(x86_64)
url='https://www.kde.org/workspaces/plasmadesktop/'
license=(LGPL)
makedepends=(frameworkintegration-opt-git kcmutils-opt-git kdecoration-opt-git extra-cmake-modules-opt-git kdoctools-opt-git kdelibs automoc4 kwayland-opt-git)
source=(${_pkgname}::git+https://anongit.kde.org/${_pkgname})
sha1sums=(SKIP )
options=(debug !strip)

pkgver() {
  cd $_pkgname
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build{,-kde4}
}

build() {
  export PATH="/opt/kde/bin:${PATH}"
  export XDG_DATA_DIRS="/opt/kde5/share:${XDG_DATA_DIRS}"

  cd build
  cmake ../${_pkgname} \
    -DCMAKE_INSTALL_PREFIX=/opt/kde \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
  cd ..

  cd build-kde4
  cmake ../${_pkgname} \
    -DCMAKE_INSTALL_PREFIX=/opt/kde \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DBUILD_TESTING=OFF \
    -DUSE_KDE4=ON
  make
}

package_oxygen-opt-git() {
  pkgdesc='KDE Oxygen style'
  depends=(frameworkintegration-opt-git kdecoration-opt-git kcmutils-opt-git kwayland-opt-git hicolor-icon-theme)
  groups=(plasma-opt)

  cd build
  make DESTDIR="$pkgdir" install
}

package_oxygen-kde4-opt-git() {
  pkgdesc='KDE Oxygen style for KDE4 applications'
  depends=(kdelibs)

  cd build-kde4
  make DESTDIR="$pkgdir" install

  # needed so that pure Qt4 apps are correctly themed
  install -d -m755 "$pkgdir"/opt/kde/lib/qt4/plugins/styles
  ln -s /opt/kde/lib/kde4/plugins/styles/oxygen-opt-git.so "$pkgdir"/opt/kde/lib/qt4/plugins/styles
}
