# Maintainer: Antonio Rojas <arojas@archlinux.org>

_pkgname=plasma-vault
pkgname=${_pkgname}-opt-git
pkgver=5.13.3
pkgrel=1
pkgdesc="Plasma applet and services for creating encrypted vaults"
arch=(x86_64)
url="https://www.kde.org/"
license=(LGPL)
groups=(plasma-opt)
depends=(plasma-workspace-opt-git networkmanager-qt-opt-git)
makedepends=(extra-cmake-modules-opt-git)
optdepends=('encfs: to use encFS for encryption' 'cryfs: to use cryFS for encryption')
source=(${_pkgname}::git+https://anongit.kde.org/${_pkgname})
sha1sums=(SKIP )
options=(debug !strip)

pkgver() {
  cd $_pkgname
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  export PATH="/opt/kde/bin:${PATH}"
  export XDG_DATA_DIRS="/opt/kde5/share:${XDG_DATA_DIRS}"

  cd build
  cmake ../${_pkgname} \
    -DCMAKE_INSTALL_PREFIX=/opt/kde \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
