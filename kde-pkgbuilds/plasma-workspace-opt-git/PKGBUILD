# $Id: PKGBUILD 328178 2018-07-10 13:33:04Z arojas $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Alexey D. <lq07829icatm at rambler.ru>

_pkgname=plasma-workspace
pkgname=(plasma-workspace-opt-git plasma-wayland-session-opt-git)
pkgver=5.13.3
pkgrel=1
pkgdesc='KDE Plasma Workspace'
arch=(x86_64)
url='https://www.kde.org/workspaces/plasmadesktop/'
license=(LGPL)
depends=(kjsembed-opt-git knotifyconfig-opt-git libksysguard-opt-git ktexteditor-opt-git libqalculate qt5-tools kde-cli-tools-opt-git bc
         xorg-xrdb xorg-xsetroot kactivitymanagerd-opt-git kholidays-opt-git xorg-xmessage xorg-xprop milou-opt-git prison-opt-git kwin
         plasma-integration-opt-git kdelibs4support-opt-git)
makedepends=(extra-cmake-modules-opt-git kdoctools-opt-git gpsd baloo-opt-git appstream-qt krunner-opt-git networkmanager-qt-opt-git kdesignerplugin-opt-git)
groups=(plasma-opt)
source=(${_pkgname}::git+https://anongit.kde.org/${_pkgname} kde.pam)
sha1sums=(SKIP SKIP )
options=(debug !strip)

pkgver() {
  cd $_pkgname
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  export PATH="/opt/kde/bin:${PATH}"
  export XDG_DATA_DIRS="/opt/kde5/share:${XDG_DATA_DIRS}"

  cd build
  cmake ../${_pkgname} \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DCMAKE_INSTALL_PREFIX=/opt/kde \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package_plasma-workspace-opt-git() {
  optdepends=('plasma-workspace-wallpapers-opt-git: additional wallpapers'
              'gpsd: GPS based geolocation' 'networkmanager-qt-opt-git: IP based geolocation'
              'kdepim-addons: displaying PIM events in the calendar'
              'appmenu-qt4: global menu support for Qt4 applications'
              'appmenu-gtk-module: global menu support for GTK2 and some GTK3 applications'
              'qt5-virtualkeyboard: virtual keyboard support in lock screen'
              'baloo-opt-git: Baloo search runner'
              'appstream-qt: package search runner')
  backup=('etc/pam.d/kde')

  cd build
  make DESTDIR="$pkgdir" install

  install -Dm644 "$srcdir"/kde.pam "$pkgdir"/etc/pam.d/kde

  # Split plasma-wayland scripts
  rm -r "$pkgdir"/opt/kde/share/wayland-sessions
}

package_plasma-wayland-session-opt-git() {
  pkgdesc='Plasma Wayland session'
  depends=(plasma-workspace-opt-git qt5-wayland kwayland-integration-opt-git xorg-server-xwayland)
  groups=()

  install -Dm644 build/plasmawayland.desktop "$pkgdir"/opt/kde/share/wayland-sessions/plasmawayland.desktop
}
