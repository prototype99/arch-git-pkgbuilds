# $Id: PKGBUILD 295962 2017-05-13 12:58:56Z arojas $
# Maintainer: Antonio Rojas <arojas@archlinux.org>

_pkgname=xdg-desktop-portal-kde
pkgname=${_pkgname}-opt-git
pkgver=5.13.3
pkgrel=1
pkgdesc='A backend implementation for xdg-desktop-portal using Qt/KF5'
arch=(x86_64)
url='https://www.kde.org/workspaces/plasmadesktop/'
license=(LGPL)
depends=(ki18n-opt-git knotifications-opt-git pipewire libepoxy kwayland-opt-git kwidgetsaddons-opt-git)
makedepends=(extra-cmake-modules-opt-git)
groups=(plasma-opt)
source=(${_pkgname}::git+https://anongit.kde.org/${_pkgname})
sha1sums=(SKIP )
options=(debug !strip)

pkgver() {
  cd $_pkgname
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  export PATH="/opt/kde/bin:${PATH}"
  export XDG_DATA_DIRS="/opt/kde5/share:${XDG_DATA_DIRS}"

  cd build
  cmake ../${_pkgname} \
    -DCMAKE_INSTALL_PREFIX=/opt/kde \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
