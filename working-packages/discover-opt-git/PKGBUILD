# $Id: PKGBUILD 328178 2018-07-10 13:33:04Z arojas $
# Maintainer: Antonio Rojas <arojas@archlinux.org> 

_pkgname=discover
pkgname=${_pkgname}-opt-git
pkgver=5.13.3
pkgrel=1
pkgdesc='KDE and Plasma resources management GUI'
arch=(x86_64)
url='https://projects.kde.org/projects/kde/workspace/discover-opt-git'
license=(LGPL)
depends=(knewstuff-opt-git kitemmodels-opt-git qt5-graphicaleffects appstream-qt archlinux-appstream-data
         hicolor-icon-theme kirigami2-opt-git)
makedepends=(extra-cmake-modules-opt-git plasma-framework-opt-git packagekit-qt5 flatpak)
optdepends=('packagekit-qt5: to manage packages from Arch Linux repositories' 'flatpak: Flatpak packages support')
groups=(plasma-opt)
source=(${_pkgname}::git+https://anongit.kde.org/${_pkgname})
sha1sums=(SKIP )
options=(debug !strip)

pkgver() {
  cd $_pkgname
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  export PATH="/opt/kde/bin:${PATH}"
  export XDG_DATA_DIRS="/opt/kde5/share:${XDG_DATA_DIRS}"

  cd build
  cmake ../${_pkgname} \
    -DCMAKE_INSTALL_PREFIX=/opt/kde \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
